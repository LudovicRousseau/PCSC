# -*- mode: m4; -*-
# Process this file with autoconf to produce a configure script

AC_PREREQ(2.58)

AC_INIT(pcsc-lite, 1.2.9-beta9)
AC_CONFIG_SRCDIR(src/pcscdaemon.c)
AM_INIT_AUTOMAKE(1.8)
AM_CONFIG_HEADER(config.h)

AC_CONFIG_MACRO_DIR(m4)

AC_CANONICAL_HOST
AC_PROG_CC
AC_C_BIGENDIAN

# Check for some target-specific stuff
case "$host" in
*-*-hpux*)
	CPPFLAGS="$CPPFLAGS -D_HPUX_SOURCE -D_XOPEN_SOURCE_EXTENDED"
	;;
*-*-solaris*)
	CPPFLAGS="$CPPFLAGS -I/usr/local/include"
	LDFLAGS="$LDFLAGS -L/usr/local/lib -R/usr/local/lib"
	need_dash_r=1
	;;
*-*-sunos4*)
	CPPFLAGS="$CPPFLAGS -DSUNOS4"
	;;
*-*-aix*)
	CPPFLAGS="$CPPFLAGS -I/usr/local/include"
	LDFLAGS="$LDFLAGS -L/usr/local/lib"
	if (test "$LD" != "gcc" && test -z "$blibpath"); then
		blibpath="/usr/lib:/lib:/usr/local/lib"
	fi
	;;
*-*-osf*)
	CPPFLAGS="$CPPFLAGS -D_POSIX_PII_SOCKET"
	;;
*-*-darwin*)
	AC_SUBST(COREFOUNDATION)
	COREFOUNDATION="-Wl,-framework,CoreFoundation"
	AC_SUBST(IOKIT)
	IOKIT="-Wl,-framework,IOKit"
	if test "$GCC" = "yes"; then
		CFLAGS="$CFLAGS -no-cpp-precomp"
	fi
	use_libusb=false
	AC_MSG_WARN([libusb disabled on Darwin for pcsc-lite])
	;;
esac

# Options
AM_MAINTAINER_MODE

# Checks for programs
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LEX
AC_PATH_PROG(TEST_MINUS_S_SH, bash)
AC_PATH_PROG(TEST_MINUS_S_SH, ksh)
AC_PATH_PROG(TEST_MINUS_S_SH, sh)

# Special check for pthread support
ACX_PTHREAD(
[
 AC_DEFINE(HAVE_PTHREAD, 1,
  [Define if you have POSIX threads libraries and header files.])
], [
 AC_MSG_ERROR([POSIX thread support required])
])

CC="$PTHREAD_CC"

AC_SUBST(PTHREAD_LIBS)
AC_SUBST(PTHREAD_CFLAGS)

# Add libtool support
AM_PROG_LIBTOOL
# Automatically update the libtool script if it becomes out-of-date
#AC_SUBST(LIBTOOL_DEPS)

# Checks for header files
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([dirent.h dl.h dlfcn.h errno.h fcntl.h getopt.h malloc.h stdlib.h string.h strings.h sys/filio.h sys/time.h syslog.h unistd.h smartcard/scf.h])

# Checks for typedefs, structures, and compiler characteristics
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

# Checks for library functions
AC_FUNC_ERROR_AT_LINE
AC_FUNC_STAT
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(daemon flock getopt_long nanosleep strerror vsnprintf)

# strlcpy, strlcat from OpenBSD
AC_CHECK_FUNCS(strlcpy strlcat)

# C Compiler features
AC_C_INLINE
if test "$GCC" = "yes"; then
	CFLAGS="-Wall -fno-common $CFLAGS"
fi

# check if the compiler support -fvisibility=hidden (GCC >= 4)
saved_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -fvisibility=hidden"
AC_MSG_CHECKING([for -fvisibility=hidden])
AC_COMPILE_IFELSE([char foo;],
	[	AC_MSG_RESULT([yes])
		SYMBOL_VISIBILITY="-fvisibility=hidden" ],
	AC_MSG_RESULT([no]))
CFLAGS="$saved_CFLAGS"
AC_SUBST(SYMBOL_VISIBILITY)

AC_SUBST(LIBDL)
AC_CHECK_LIB(dl, dlopen, [LIBDL="$LIBDL -ldl" ac_cv_func_dlopen_ldl=yes], ac_cv_func_dlopen_ldl=no)

# Use 'uname' output as an architecture define
uname=`uname`
case "$uname" in
Darwin)
	uname=MacOS
	;;
esac
AC_DEFINE_UNQUOTED(PCSC_ARCH, "$uname", [PC/SC target architecture])
AC_DEFINE_UNQUOTED(MSC_ARCH, "$uname", [MuscleCard target architecture])

# See if socket() is found from libsocket
AC_CHECK_LIB(socket, socket, [LIBS="$LIBS -lsocket"])

AC_SUBST(LIBUSB)
# --enable-libusb=DIR
AC_ARG_ENABLE(libusb,
  AC_HELP_STRING([--enable-libusb=PATH],[enable USB hotplug using libusb]),
	[
		case ${enableval} in
			"" | "yes" | "YES")
				;;
			"no" | "NO")
				use_libusb=false
				;;
			*)
				use_libusb=false
				libusb_prefix=${enableval}
				AC_CHECK_LIB(usb, usb_init, [LIBUSB="$LIBUSB -lusb" use_libusb=true ], [ use_libusb=false ], "-L${libusb_prefix}/lib")
				AC_CHECK_HEADERS(${libusb_prefix}/include/usb.h, [ use_libusb=true ], [ use_libusb=false ])
				if test x$use_libusb = xtrue; then
					CPPFLAGS="$CPPFLAGS -I${libusb_prefix}/include"
					LDFLAGS="$LDFLAGS -L${libusb_prefix}/lib"
				fi
				;;
		esac
	]
)

# if use_libusb is not yet defined
if test x$use_libusb = x; then
	# check if libusb is available
	AC_CHECK_HEADERS(usb.h, [ use_libusb=true ], [ use_libusb=false ])
	AC_CHECK_LIB(usb, usb_init, [LIBUSB="$LIBUSB -lusb" use_libusb=true ], [ use_libusb=false ])
fi

AC_MSG_RESULT([enable libusb support         : $use_libusb])

if test x$use_libusb = xtrue; then
  AC_DEFINE(HAVE_LIBUSB,1, [Libusb is available])
fi

# --enable-usbdropdir=DIR
AC_ARG_ENABLE(usbdropdir,
  AC_HELP_STRING([--enable-usbdropdir=DIR],[directory containing USB
				  drivers (default /usr/local/pcsc/drivers)]),
[usbdropdir="${enableval}"], [usbdropdir=false])
if test x${usbdropdir} = xfalse ; then
	if test "x$prefix" != xNONE; then
		usbdropdir="$prefix/pcsc/drivers"
	else
		usbdropdir="$ac_default_prefix/pcsc/drivers"
	fi
fi
AC_MSG_RESULT([enable USB drop directory     : $usbdropdir])
AC_DEFINE_UNQUOTED(PCSCLITE_HP_DROPDIR, "$usbdropdir", [directory containing USB drivers])

# --enable-debugatr
AC_ARG_ENABLE(debugatr,
  AC_HELP_STRING([--enable-debugatr],[enable ATR debug messages from pcscd]),
[ case "${enableval}" in
  yes)	debugatr=true ;;
  no)	debugatr=false ;;
  *) AC_MSG_ERROR([bad value ${enableval} for --enable-debugatr]) ;;
esac], [debugatr=false])

AC_MSG_RESULT([enable ATR parsing messages   : $debugatr])

if test x${debugatr} = xtrue ; then
  AC_DEFINE(ATR_DEBUG, 1, [display ATR parsing debug messages.])
fi

# --enable-scf
AC_ARG_ENABLE(scf,
  AC_HELP_STRING([--enable-scf],[use SCF for reader support]),
[ case "${enableval}" in
  yes)	scf=true ;;
  no)	scf=false ;;
  *) AC_MSG_ERROR([bad value ${enableval} for --enable-scf]) ;;
esac], [scf=false])

AC_SUBST(LIBSMARTCARD)
if test x$scf = xtrue -a x$ac_cv_header_smartcard_scf_h = xyes; then
  AC_CHECK_LIB(smartcard, SCF_strerror, [LIBSMARTCARD="$LIBSMARTCARD -lsmartcard" scf=true], scf=false)
else
  scf=false
fi
AC_MSG_RESULT([enable SCF reader support     : $scf])
AM_CONDITIONAL(HAVE_SCF, test x$scf = xtrue)

# --enable-confdir=DIR
AC_ARG_ENABLE(confdir,
AC_HELP_STRING([--enable-confdir=DIR],[directory containing reader.conf
				(default /etc)]),
[confdir="${enableval}"], [confdir=false])

if test x${confdir} != xfalse ; then
	AC_MSG_RESULT([enable confdir                : $confdir])
else
	confdir="/etc"
	AC_MSG_RESULT([enable confdir                : /etc (default)])
fi

# --enable-runpid=FILE
AC_ARG_ENABLE(runpid,
  AC_HELP_STRING([--enable-runpid=FILE],[file containing pcscd pid]),
[runpid="${enableval}"], [runpid=false])

AC_MSG_RESULT([enable runpid                 : $runpid])

# HAVE_RUNPID is for pcscd.8
if test x${runpid} != xfalse ; then
	AC_DEFINE_UNQUOTED(USE_RUN_PID, "$runpid", [file containing pcscd pid])
	AC_SUBST(HAVE_RUNPID,'')
else
	AC_SUBST(HAVE_RUNPID,'.\" ')
fi

# --enable-ipcdir=DIR
AC_ARG_ENABLE(ipcdir,
  AC_HELP_STRING([--enable-ipcdir=DIR],[directory containing IPC files
				  (default /var/run)]),
[ipcdir="${enableval}"], [ipcdir=false])

if test x${ipcdir} != xfalse ; then
	AC_MSG_RESULT([enable ipcdir                 : $ipcdir])

	AC_DEFINE_UNQUOTED(USE_IPCDIR, "$ipcdir", [directory containing IPC files (default /var/run)])
else
	AC_MSG_RESULT([enable ipcdir                 : /var/run (default)])
fi

# --enable-extendedapdu
AC_ARG_ENABLE(extendedapdu,
	AC_HELP_STRING([--enable-extendedapdu], [support of extended APDU (for APDU  256B < size < 32KB)]),
	[ case "${enableval}" in
		yes) extendedapdu=true ;;
		no) extendedapdu=false ;;
		*) AC_MSG_ERROR([bad value ${enableval} for --enable-extendedapdu]) ;;
	esac], [extendedapdu=false])

AC_MSG_RESULT([enable extended APDU          : $extendedapdu])

if test x${extendedapdu} = xtrue ; then
  extended_apdu="#define PCSCLITE_ENHANCED_MESSAGING"
else
  extended_apdu="#undef PCSCLITE_ENHANCED_MESSAGING"
fi

# --enable-muscledropdir=DIR
AC_ARG_ENABLE(muscledropdir,
	AC_HELP_STRING([--enable-muscledropdir=DIR],[directory containing
		MuscleCard bundles (default /usr/local/pcsc/services)]),
	[muscledropdir="${enableval}"], [muscledropdir=false])
if test x${muscledropdir} = xfalse ; then
	if test "x$prefix" != xNONE; then
		muscledropdir="$prefix/pcsc/services"
	else
		muscledropdir="$ac_default_prefix/pcsc/services"
	fi
fi
AC_MSG_RESULT([enable MuscleCard bundles dir : $muscledropdir])
AC_DEFINE_UNQUOTED(MSC_SVC_DROPDIR, "$muscledropdir", [directory containing MuscleCard bundles])

# --enable-musclecarddebug
AC_ARG_ENABLE(musclecarddebug,
	AC_HELP_STRING([--enable-musclecarddebug],[enable libmusclecard debug messages through pcscd]),
[ case "${enableval}" in
  yes)
    musclecarddebug=true ;;
  no)
    musclecarddebug=false ;;
  *) AC_MSG_ERROR([bad value ${enableval} for --enable-musclecarddebug]) ;;
 esac], [musclecarddebug=true])
AC_MSG_RESULT([enable MuscleCard debug       : $musclecarddebug])
if test x${musclecarddebug} = xfalse ; then
	AC_DEFINE(NO_MSC_DEBUG, 1, [disable MuscleCard debug messaging.])
fi

CPPFLAGS="-I\${top_srcdir}/src $CPPFLAGS"

# HOST_TO_CCID
if test "x$ac_cv_c_bigendian" = "xyes"; then
	AC_SUBST(host_to_ccid_16, ['((((x) >> 8) & 0xFF) + ((x & 0xFF) << 8))'])
	AC_SUBST(host_to_ccid_32, ['((((x) >> 24) & 0xFF) + (((x) >> 8) & 0xFF00) + ((x & 0xFF00) << 8) + (((x) & 0xFF) << 24))'])
	AC_MSG_RESULT([plateform endianess           : big endian])
else
	AC_SUBST(host_to_ccid_16, ['(x)'])
	AC_SUBST(host_to_ccid_32, ['(x)'])
	AC_MSG_RESULT([plateform endianess           : little endian])
fi

# Setup dist stuff
AC_SUBST(usbdropdir)
AC_SUBST(confdir)
AC_SUBST(runpid)
AC_SUBST(extended_apdu)
AC_SUBST(muscledropdir)
AC_SUBST(host_to_ccid_16)
AC_SUBST(host_to_ccid_32)
AS_AC_EXPAND(confdir_exp,$confdir)
AS_AC_EXPAND(sysconfdir_exp,$sysconfdir)
AS_AC_EXPAND(sbindir_exp,$sbindir)
AS_AC_EXPAND(localstatedir_exp,$localstatedir)

# Write Makefiles
AC_CONFIG_FILES([
Makefile
m4/Makefile
doc/Makefile
doc/doxygen.conf
doc/pcscd.8
doc/reader.conf.5
doc/example/Makefile
etc/Makefile
etc/pcscd.init
etc/reader.conf
etc/update-reader.conf
src/Makefile
src/libpcsclite.pc
src/PCSC/pcsclite.h
src/PCSC/reader.h
src/utils/Makefile
win32/Makefile
libmusclecard/Makefile
libmusclecard/doc/Makefile
libmusclecard/src/libmusclecard.pc
libmusclecard/src/Makefile
libmusclecard/utils/Makefile
libmusclecard/utils/bundleTool/bundleTool.8
libmusclecard/utils/bundleTool/Makefile
])
AC_OUTPUT

